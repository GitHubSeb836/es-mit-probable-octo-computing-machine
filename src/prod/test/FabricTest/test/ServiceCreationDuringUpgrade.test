votes 10 20 30
namingservice 1 3 1
cmservice 3 1
cleantest

set HealthReportSendInterval 2
set HealthReportRetrySendInterval 2
set UpgradeStatusPollInterval 2
set UpgradeHealthCheckInterval 2

+10
+20
+30
verify

# 
# Application version V1
#
app.add versionV1 TestApp V1

app.clear versionV1

app.servicepack versionV1 ServicePackageX version=V1

app.servicepack versionV1 ServicePackageY version=V1

app.servicetypes versionV1 ServicePackageX ServiceTypeX stateful persist

app.servicetypes versionV1 ServicePackageY ServiceTypeY stateful persist

app.codepack versionV1 ServicePackageX CodeX types=ServiceTypeX version=V1

app.codepack versionV1 ServicePackageY CodeY types=ServiceTypeY version=V1

app.upload versionV1

# 
# Application version V2 
#

app.add versionV2 TestApp V2

app.clear versionV2

app.servicepack versionV2 ServicePackageX version=V2

app.servicepack versionV2 ServicePackageY version=V1

app.servicetypes versionV2 ServicePackageX ServiceTypeX stateful persist

app.servicetypes versionV2 ServicePackageY ServiceTypeY stateful persist

app.codepack versionV2 ServicePackageX CodeX types=ServiceTypeX version=V2

app.codepack versionV2 ServicePackageY CodeY types=ServiceTypeY version=V1

app.upload versionV2

#
# Provision application version V1
#
provisionapp versionV1

#
# Provision application version V2
#
provisionapp versionV2

#
# Create application version V1
#
createapp fabric:/app1 TestApp V1

verify

#
# Upgrade application from version V1 to V2 and delay the create service request so
# that it arrives after the upgrade completes at the FM
# 

addbehavior b1 * * InnerCreateServiceRequest 1.0 10

upgradeapp fabric:/app1 V2 Rolling upgrademode=auto

createservice fabric:/app1/svc1 ServiceTypeX y 1 3 persist appname=fabric:/app1

verify

removebehavior b1

#
# Service creation should not be blocked during health checks after last UD
#

upgradeapp fabric:/app1 V1 Rolling upgrademode=manual

verifyupgradeapp fabric:/app1 upgradedomains=10_ud

#
# Service type not affected by upgrade should succeed
#

createservice fabric:/app1/svcY ServiceTypeY y 1 3 persist appname=fabric:/app1

upgradeappmovenext fabric:/app1

verifyupgradeapp fabric:/app1 upgradedomains=10_ud,20_ud

addbehavior b2 * * NodeUpgradeReply

upgradeappmovenext fabric:/app1

!pause 10

updateappupgrade fabric:/app1 upgrademode=monitored failaction=rollback healthcheckwait=60 healthcheckretry=30

removebehavior b2

!pause 20

set NamingOperationTimeout 10

createservice fabric:/app1/svc2 ServiceTypeX y 1 1 persist appname=fabric:/app1

updateappupgrade fabric:/app1 healthcheckwait=0

verifyupgradeapp fabric:/app1

verify

deleteapp fabric:/app1

verify

!q
