set DummyPLBEnabled true
set FullRebuildWaitDuration 5
set UserReplicaRestartWaitDuration 9999
set QuorumLossWaitDuration 5
votes 5 10 20
cmservice 0 0
namingservice 1 3 1
fmservice 1 1
cleantest

set NamingOperationRetryTimeout 30
set NamingOperationTimeout 15
set FMPlacementConstraints fmservice==true

# Use placement constraint to make sure 10 is chosen as primary

+10 nodeprops=fmservice:true
+20 

!wait
!waitforstate FM.Replica.Role.FMService.10 Primary
verify

+5 nodeprops=fmservice:true
+30 nodeprops=fmservice:true
+40 nodeprops=fmservice:true
+50 nodeprops=fmservice:true
verify

# Create a persisted stateful service with 3 replicas
createname fabric:/persisted
createservice fabric:/persisted TestPersistedStoreServiceType y 1 3 persist
verify

!waitforstate FM.Replica.Role.fabric:/persisted.50 Primary
!waitforstate FM.Replica.Role.fabric:/persisted.40 Secondary
!waitforstate FM.Replica.Role.fabric:/persisted.30 Secondary

clientput fabric:/persisted 10 10Data1
!wait 

# Bring down all the replicas
addbehavior b1 * * DoReconfiguration
-50 removedata
-40 removedata
-30

# Add other node so FM's next location won't overlap the service's locations
+60 nodeprops=fmservice:true
!wait

# Down FM to cause a rebuild (there's only 1 FM replica)
-10
!waitforstate FM.Replica.Role.FMService.60 Primary
!waitforstate FM.Replica.State.FMService.60 Ready
!waitforstate FM.IsReady true

# Bring up a secondary so the inbuild failover unit is created
+30 nodeprops=fmservice:true

# Check the inbuild failover unit is created
!waitforstate FM.FT.IsInRebuild.fabric:/persisted true

# Invoke NodeStateRemoved on the remaining replicas
set NamingOperationRetryTimeout 5
set NamingOperationTimeout 5

nodestateremoved 40 
nodestateremoved 50 

# Check the inbuild failover unit is still present
!waitforstate FM.FT.IsInRebuild.fabric:/persisted true

# Confirm the failover unit is recreated
!waitforstate FM.FT.IsInRebuild.fabric:/persisted false
!waitforstate FM.Replica.Role.fabric:/persisted.30 Primary

+10
+40
+50
!wait

clientput fabric:/persisted 90 90Data1
!wait 
verify

set NamingOperationRetryTimeout 60
set NamingOperationTimeout 10

deleteservice fabric:/persisted

verify
!q
