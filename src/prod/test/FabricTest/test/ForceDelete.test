#
# [owner] linggeng
# This tests forceful delete service and application
#

votes 10 20 30
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set MaxOperationTimeout 120
set NamingOperationTimeout 40
set NamingOperationRetryTimeout 120
set IsSingletonReplicaMoveAllowedDuringUpgrade false
set DeactivationFailedRetryIntervalRange 0

+10 nodeprops=system:true
+20 nodeprops=system:false
+30 nodeprops=system:true
verify

#
# Create application
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicepack version10 ServicePackageB version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA stateful persist
app.servicetypes version10 ServicePackageB ServiceTypeB stateless

app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA version=1.0
app.codepack version10 ServicePackageB CodeB1 types=ServiceTypeB version=1.0
app.upload version10
provisionapp version10

createapp fabric:/app1 TestApp 1.0
verify

######################################################################
# Scenario 1: Forceful delete service
# RA should terminate the app host and the delete should complete
######################################################################

createservice fabric:/app1/test1 TestApp_App0:ServicePackageA:ServiceTypeA y 1 1 persist constraint=(system!=true) appname=fabric:/app1
createservice fabric:/app1/test2 TestApp_App0:ServicePackageB:ServiceTypeB n 1 1 constraint=(system!=true) appname=fabric:/app1
verify

setcodepackagekillpending 20 TestApp_App0:ServicePackageA#CodeA1
deleteservice fabric:/app1/test1 isForce
verify

setcodepackagekillpending 20 TestApp_App0:ServicePackageB#CodeB1
deleteservice fabric:/app1/test2 isForce
verify

nameexists fabric:/app1/test1 false
nameexists fabric:/app1/test2 false

#######################################################################
# Scenario 2: Replica down
# Deleting quorum of replicas suffices a delete service
# Forceful delete service while replica is Down
#######################################################################

createservice fabric:/app1/test3 TestApp_App0:ServicePackageA:ServiceTypeA y 1 3 persist minreplicasetsize=3 appname=fabric:/app1
verify

deactivatenode 20 Restart
!waitforstate FM.Replica.IsUp.fabric:/app1/test3.20 false
!waitforstate FM.Replica.IsUp.fabric:/app1/test3.10 true
!waitforstate FM.Replica.IsUp.fabric:/app1/test3.30 true

!waitforstate FM.IsReady true
gfum

deleteservice fabric:/app1/test3
activatenode 20
!waitforstate FM.Replica.State.fabric:/app1/test3.20 Dropped
!waitforstate RA.FT.State.fabric:/app1/test3.20 Closed

verify

createservice fabric:/app1/test4 TestApp_App0:ServicePackageA:ServiceTypeA y 1 1 persist constraint=(system!=true) appname=fabric:/app1
verify

deactivatenode 20 Restart
!waitforstate FM.Replica.IsUp.fabric:/app1/test4.20 false

!waitforstate FM.IsReady true
gfum

deleteservice fabric:/app1/test4 isForce
activatenode 20

verify

nameexists fabric:/app1/test3 false
nameexists fabric:/app1/test4 false

#######################################################################
# Scenario 3: Convert stuck delete service to forceful delete
# The normal one should convert and the delete should complete
#######################################################################

createservice fabric:/app1/test5 TestApp_App0:ServicePackageA:ServiceTypeA y 1 1 persist constraint=(system!=true) appname=fabric:/app1
createservice fabric:/app1/test6 TestApp_App0:ServicePackageB:ServiceTypeB n 1 1 constraint=(system!=true) appname=fabric:/app1
verify

addbehavior b1 * * ReplicaClose
deleteservice fabric:/app1/test5 errors=Timeout
deleteservice fabric:/app1/test6 errors=Timeout
query GetApplicationServiceList ApplicationName=fabric:/app1 verify ServiceName=fabric:/app1/test5,ServiceStatus=Deleting,ServiceTypeName=ServiceTypeA,ServiceManifestVersion=1.0
query GetApplicationServiceList ApplicationName=fabric:/app1 verify ServiceName=fabric:/app1/test6,ServiceStatus=Deleting,ServiceTypeName=ServiceTypeB,ServiceManifestVersion=1.0

setcodepackagekillpending 20 TestApp_App0:ServicePackageA#CodeA1
set NamingOperationTimeout 360
deleteservice fabric:/app1/test5 isForce
setcodepackagekillpending 20 TestApp_App0:ServicePackageB#CodeB1
deleteservice fabric:/app1/test6 isForce
verify

nameexists fabric:/app1/test5 false
nameexists fabric:/app1/test6 false

#######################################################################
# Scenario 4: Forceful delete application while ReplicaClose blocked
#######################################################################

set NamingOperationTimeout 60

createservice fabric:/app1/test7 TestApp_App0:ServicePackageA:ServiceTypeA y 1 1 persist constraint=(system!=true) appname=fabric:/app1
createservice fabric:/app1/test8 TestApp_App0:ServicePackageB:ServiceTypeB n 1 1 constraint=(system!=true) appname=fabric:/app1
verify

# Let CM be aware service is deleting
deleteservice fabric:/app1/test7 errors=Timeout

setcodepackagekillpending 20 TestApp_App0:ServicePackageA#CodeA1
setcodepackagekillpending 20 TestApp_App0:ServicePackageB#CodeB1
set NamingOperationTimeout 360
deleteapp fabric:/app1 isForce
verify

nameexists fabric:/app1/test7 false
nameexists fabric:/app1/test8 false

#######################################################################
# Scenario 5: Convert stuck delete application to forceful delete
#######################################################################

set NamingOperationTimeout 60
createapp fabric:/app2 TestApp 1.0
verify

createservice fabric:/app2/test1 TestApp_App1:ServicePackageA:ServiceTypeA y 1 1 persist constraint=(system!=true) appname=fabric:/app2
verify

deleteapp fabric:/app2 error=Timeout
query GetApplicationList verify ApplicationName=fabric:/app2,ApplicationTypeName=TestApp,ApplicationTypeVersion=1.0,ApplicationStatus=Deleting

setcodepackagekillpending 20 TestApp_App1:ServicePackageA#CodeA1
set NamingOperationTimeout 360
deleteapp fabric:/app2 isForce
verify

nameexists fabric:/app2/test1 false
nameexists fabric:/app2 false

removebehavior b1
-*
!q
