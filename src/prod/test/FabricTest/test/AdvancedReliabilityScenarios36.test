#   [owner:sumukhs]
#   This test validates the following scenario associated with the bug 1294279
#       1. Primary is building an Idle
#       2. Primary goes down before build is completed and new primary is chosen
#       3. As part of GetLSN, idle receives new Epoch
#       4. New Primary starts building Idle and a replication operation reaches the idle before StartCOPY from the new primary
#   Refer to the bug details for the cause and the fix
#

set DummyPLBEnabled true
set UserReplicaRestartWaitDuration 100
votes 10 20 30
cmservice 0 0
namingservice 1 3 2
cleantest
+10
+20
+30
verify

+40
+50
+60
verify 

createname fabric:/persistedtestcase2
createservice fabric:/persistedtestcase2 TestPersistedStoreServiceType y 1 5 persist

# Verify current replica set
!waitforstate FM.Replica.Role.fabric:/persistedtestcase2.60 Primary
!waitforstate FM.Replica.Role.fabric:/persistedtestcase2.50 Secondary
!waitforstate FM.Replica.Role.fabric:/persistedtestcase2.40 Secondary

verify

addbehavior b1 * * ReplicatorBuildIdleReplicaReply
reportfault 40 fabric:/persistedtestcase2 transient 
#   1. Primary is building idle (60 building 40)
!waitforstate RA.Replica.State.fabric:/persistedtestcase2.60.40 IB
!pause 10

addbehavior b2 * 50 GetLSNReply
#   2. Primary goes down before build is completed and new primary is chosen (50 will be new primary)
-60

#   3. As part of GetLSN, idle receives new Epoch
removebehavior b1
!waitforstate FM.Replica.Role.fabric:/persistedtestcase2.50 Primary
!waitforstate RA.FT.ReconfigStage.fabric:/persistedtestcase2.50 Phase1_GetLSN 

addbehavior b3 * 50 DoReconfiguration 
removebehavior b2

#   4. New Primary starts building Idle and a replication operation reaches the idle before StartCOPY from the new primary
addbehavior b4 50 40 StartCopy
addbehavior b5 40 * ReplicationAck
addbehavior b6 40 * RequestAck

+60 

removebehavior b3
!waitforstate RA.Replica.State.fabric:/persistedtestcase2.50.40 IB

clientput fabric:/persistedtestcase2 10 10Data1
clientput fabric:/persistedtestcase2 20 20Data2
!wait
!pause 5
removebehavior b4
!pause 5
removebehavior b5
removebehavior b6
verify 

!q
