#
# [owner] MMohsin, AnuragG
#
# This test increases/decreases the number of UDs during an upgrade. New UDs should be added
# and non-existent UDs should be dynamically removed from the list of UDs.
#

votes 10
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set UpgradeStatusPollInterval 5
set NamingOperationTimeout 15
set NamingOperationRetryTimeout 180
set ExpectedReplicaUpgradeDuration 3600
set IsSingletonReplicaMoveAllowedDuringUpgrade true

+10 ud=A
+20 ud=C
+30 ud=E
verify

+40 ud=G
+50 ud=I
+60 ud=K
verify

#
# Application version 1.0
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackageA version=1.0
app.servicetypes version10 ServicePackageA ServiceTypeA stateful
app.codepack version10 ServicePackageA CodeA1 types=ServiceTypeA version=1.0
app.upload version10
provisionapp version10

#
# Application version 1.1
#
app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackageA version=1.1
app.servicetypes version11 ServicePackageA ServiceTypeA stateful
app.codepack version11 ServicePackageA CodeA1 types=ServiceTypeA version=1.1
app.upload version11
provisionapp version11

# Create application version 1.0
createapp fabric:/MyApp TestApp 1.0
verify

# Create services
createservice fabric:/MyApp/Volatile ServiceTypeA y 3 7 appname=fabric:/MyApp
verify

#
# Perform application upgrade from version 1.0 to 1.1
#
upgradeapp fabric:/MyApp 1.1 Rolling upgrademode=manual

verifyupgradeapp fabric:/MyApp upgradedomains=A
upgradeappmovenext fabric:/MyApp

# Add UD B
+25 ud=B

verifyupgradeapp fabric:/MyApp upgradedomains=A,B
upgradeappmovenext fabric:/MyApp

verifyupgradeapp fabric:/MyApp upgradedomains=A,B,C
upgradeappmovenext fabric:/MyApp

# Remove UD C
-20 removedata
!waitforstate FM.Node.IsUp.20 false
nodestateremoved 20

verifyupgradeapp fabric:/MyApp upgradedomains=A,B,E
upgradeappmovenext fabric:/MyApp

# Add UD H
+45 ud=H

verifyupgradeapp fabric:/MyApp upgradedomains=A,B,E,G
upgradeappmovenext fabric:/MyApp

# Remove UD I
-50 removedata
!waitforstate FM.Node.IsUp.50 false
nodestateremoved 50

verifyupgradeapp fabric:/MyApp upgradedomains=A,B,E,G,H

# Remove UD K
-60 removedata
!waitforstate FM.Node.IsUp.60 false
nodestateremoved 60

verifyupgradeapp fabric:/MyApp

verify

-*

!q
